module(mirth-build.main)

import(std.prelude)
import(std.str)
import(std.byte)
import(std.path)
import(std.maybe)
import(std.list)
import(std.either)
import(std.ctypes)

import(std.posix)

import(ini-parser.parser)

import(mirth-build.options)
import(mirth-build.package-data)

data(Error,
    UnknownKey -> Str,
    MissingKey,
    MissingValue)

def(meta-parsefn, 
    data:PackageDataBuilder 
    name:Maybe(Str)
    value:Maybe(Str)
    --
    data:PackageDataBuilder
    error:Maybe(mirth-build.main.Error),

    name> match(
        Some -> dup "name" == if(
	    drop value> data> name! >data None, 
	    UnknownKey Some value> drop 
	),
	None -> value> drop None 
    ) >error
)

external(
  file_exists [ CStr -- Bool ]
  dir_exists [ CStr -- Bool ]
)

def(dir-exists?, Path -- Bool,
    >Str with-cstr(dir_exists))

def(file-exists?, Path -- Bool,
    >Str with-cstr(file_exists))

def(pull-git-source, dir:Str url:Str -- Path,
    "build" >Path
    dir> >Path join dup dir-exists? not if(
        dup >Str url> Str("git clone " ; ; " " ; ;) run-program drop,	
	url> drop
    ))

def(do-dependency, name:Str value:Str data:PackageDataBuilder -- data:PackageDataBuilder error:Maybe(mirth-build.main.Error),
    name> dup >dir value> >url pull-git-source

    data> dependencies(
        dip(
	    "src" >Path join >path >name
	        None >compiler-flags
		None >linker-flags
	    GCCFlags.GCCFlags >c
	    L0 >dependencies
	    L0 >bin 
	    PackageData.PackageData
	) cons
    ) >data

    None >error
)

def(dependencies-parsefn, 
    data:PackageDataBuilder 
    name:Maybe(Str)
    value:Maybe(Str)
    --
    data:PackageDataBuilder
    error:Maybe(mirth-build.main.Error),
    
    name> match(
        Some -> >name 
	value> match(
	    Some -> >value do-dependency,
	    None -> name> drop mirth-build.main.Error.MissingValue Some >error
	),
	None -> value> drop None >error
    ))

def(bin-parsefn, 
    data:PackageDataBuilder 
    name:Maybe(Str)
    value:Maybe(Str)
    --
    data:PackageDataBuilder
    error:Maybe(mirth-build.main.Error),

    name> match(
        Some -> value> match(
	    Some -> pack2 data> bin:cons >data None >error,
	    None -> drop mirth-build.main.Error.MissingValue Some >error),
	None -> value> drop None >error))

def(package-parsefn, 
    data:PackageDataBuilder 
    section:Maybe(Str) 
    name:Maybe(Str) 
    value:Maybe(Str) 
    -- 
    data:PackageDataBuilder 
    error:Maybe(mirth-build.main.Error),

    section> match (
        Some -> 
	  dup "package" == if(
	      meta-parsefn, 
	      dup "dependencies" == if(
	          dependencies-parsefn,
		  dup "bin" == if(
		      bin-parsefn,
		      dup "c" == if(
		          c-parsefn,
			  name> drop value> drop
		          None >error)))) drop,
	None -> name> drop value> drop None >error))

def(c-parsefn, 
    data:PackageDataBuilder 
    name:Maybe(Str)
    value:Maybe(Str)
    --
    data:PackageDataBuilder
    error:Maybe(mirth-build.main.Error),

    name> match(
        Some -> dup "linker-flags" == if(
	    drop value> @data:c:linker-flags! None, 
	    dup "compiler-flags" == if(
	        drop value> @data:c:compiler-flags! None,
		UnknownKey Some value> drop
	    )
	),
	None -> value> drop None
    ) >error
)

def(make-cli-package-arg, PackageData -- Str, \(package ->
    Str(" -p " ; package name ; ":" ; package path >Str ;)))

def(make-bin-mirth-arg, [Str Str] -- Str, 
    unpack2 >source >bin
    Str("-o bin/" ; bin> ; ".c" ; " " ; source> ;))

def(make-bin-gcc-arg, [Str Str] -- Str,
    unpack2 drop dup 
    Str("-o bin/" ; ; " bin/" ; ; ".c" ;))

external(
  system [ CStr -- CInt ]
)

def(run-program, Str -- Int,
    with-cstr(system) >Int)

def(build-mirth-bin-csrc, packages:Str bin:[Str Str] --,
    Str("mirth" ; packages> ; " " ; bin> make-bin-mirth-arg ;) run-program drop)

def(build-mirth-bin-gcc, compiler-flags:Str linker-flags:Str bin:[Str Str] --,
    Str("gcc" ; " " ; compiler-flags> ; " " ; " " ; bin> make-bin-gcc-arg ; " " ; linker-flags> ;) run-program drop
)

def(main, +World -- +World,
    parse-args drop
    "mirth-build.ini" >Path PackageDataBuilder.new path(drop "src" >Path Some) ini-parse(package-parsefn) drop  
    build match(
        Left -> show panic!,
	Right -> \(package ->
	    package dependencies map(make-cli-package-arg) fold(cat) unwrap-or("") package make-cli-package-arg cat

	    package bin for(>bin dup >packages build-mirth-bin-csrc) drop

	    package c linker-flags unwrap-or("")
	    package c compiler-flags unwrap-or("")

	    package bin for(>bin dup2 >compiler-flags >linker-flags build-mirth-bin-gcc) drop2
	)
    )
)
